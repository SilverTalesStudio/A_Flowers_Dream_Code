title: DialogoPrueba
tags: 
position: -276.8234100341797,-89.41638660430908
---
// sets background image to sprite called "bg_office"
<<Scene bg_office>>
// start playing audioclip "ambient_birds" at 50% volume, loop forever
<<PlayAudio ambient_birds 0.5 loop>>

<<set $gender to get_player_gender()>>
<<set $name to get_player_name()>>
A VISUAL NOVEL EXAMPLE

// adds actor named as player selected using sprite "cool-girl", placed in left-half + center of screen, with green text label
<<Act {$name} cool-girl left center green>>


{$name}: Hola Pietro, [select value={$gender} M="encantado" F="encantada" NB="encantade" /] de conocerte

// adds actor "Adam" with sprite "spt_pietro_neutral" off-screen right, with blue-ish text label
<<Act Adam spt_pietro_neutral 1.25 center #226677>>
<<Add_npc_likeability NPC_abuela -2>>
// animate Adam into new position in right-half + center, within 0.5 seconds
<<Move Adam right center 0.5>>
<<PlayAudio music_funny 1.0 loop>>

Adam: Hola {$name}! Tengo una pregunta para ti!
{$name}: Oh no... Mi simpatia con abuela es {get_npc_likeability("NPC_abuela")}

// horizontally flips {$name} actor to face Adam
<<Flip {$name}>>

// Yarn shortcut choices, an easy way to make branching options
-> {$name} gets upset with Adam
	<<Shake {$name} 1.0>>
	{$name}: WHAT DO YOU WANT???
	Adam: I just wanted to see if you liked using Yarn Spinner.
	{$name}: ... It's fine. Thanks. Now leave me alone.
-> Adam asks {$name} a question
	Adam: Do you think Yarn Spinner is easy to use?
	{$name}: Not when you're bothering me.
	Adam: Oh, I didn't realize you were busy. Sorry.

// hide Adam actor and remove from the scene
<<Hide Adam>>
// Stop all sound playback
<<StopAudioAll>>

{$name}: Ok, now to finally write that visual novel.
<<Flip {$name}>>
<<PlayAudio music_romantic>>
{$name}: I can finally write my epic romance about -
<<StopAudioAll>>
<<Flip {$name}>>
<<wait 1.5>>
<<Flip {$name}>>
{$name}: Wait... where's my laptop? It's not here.
<<Shake {$name}>>
{$name}: Adam! Have you seen my laptop???

<<PlayAudio music_funny>>
<<Flip {$name}>>
<<Move {$name} 1.25 center 1.0>>
{$name}: ADDDDAAAMMMMMMM!!!

// fade to black, from 0% opacity to 100% opacity, in 2.0 seconds
<<Fade black 0.0 1.0 2.0>>
THE END.
===